set shell := ["bash", "-c"]

# Display this help message
help:
    @just --list

# Run python unittests
test:
    python -m unittest discover -t . -s tests -vv

# Lint frontend using eslint
[private]
lint-eslint:
    cd src/frontend && yarn lint

# Lint python source files using black
[private]
lint-black:
    black --check $(git ls-files "*.py")

# Lint python source files using pylint
[private]
lint-pylint:
    pylint $(git ls-files "*.py")

# Lint python source files using pylint, skipping docstring checks
[private]
lint-pylint-dev:
    pylint $(git ls-files "*.py") --disable missing-module-docstring,missing-class-docstring,missing-function-docstring,fixme

# Lint all source files
lint: lint-eslint lint-black lint-pylint

# Lint all source files, skipping docstring and TODO checks
lint-dev: lint-eslint lint-black lint-pylint-dev

# Lint and fix all source files
lint-fix:
    cd src/frontend && yarn lint:fix
    black $(git ls-files "*.py")
    pylint $(git ls-files "*.py")

# Run the backend server
backend-dev:
    cd src && python -m backend

# Run the backend server on a non Raspberry Pi device
backend-dev-norpi:
    cd src && python -m backend --no-ipc --no-camera --no-din-midi

# Run the vite frontend server
frontend-dev:
    cd src/frontend && yarn dev
